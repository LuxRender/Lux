#
# Try to find the FreeImage library and include path.
# Once done this will define
#
# FREEIMAGE_FOUND
# FREEIMAGE_INCLUDE_PATH
# FREEIMAGE_LIBRARY
# 

# Lookup user provide path first
FIND_PATH( FREEIMAGE_INCLUDE_PATH
	NAMES FreeImage.h freeimage.h
	PATHS
	${FREEIMAGE_ROOT_DIR}
	PATH_SUFFIXES include
	DOC "The directory where FreeImage.h resides")
FIND_PATH( FREEIMAGE_INCLUDE_PATH
	NAMES FreeImage.h freeimage.h
	PATHS
	/usr
	/usr/local
	/sw
	/opt/local
	PATH_SUFFIXES include
	DOC "The directory where FreeImage.h resides")
FIND_LIBRARY( FREEIMAGE_LIBRARY
	NAMES FreeImage freeimage
	PATHS
	${FREEIMAGE_ROOT_DIR}
	PATH_SUFFIXES lib64 lib
	DOC "The FreeImage library")
FIND_LIBRARY( FREEIMAGE_LIBRARY
	NAMES FreeImage freeimage
	PATHS
	/usr
	/usr/local
	/sw
	/opt/local
	PATH_SUFFIXES lib64 lib
	DOC "The FreeImage library")

SET(FREEIMAGE_LIBRARIES ${FREEIMAGE_LIBRARY})

IF (FREEIMAGE_INCLUDE_PATH AND FREEIMAGE_LIBRARY)
	SET(FREEIMAGE_FOUND TRUE)
	IF(NOT APPLE)
########################### PNG   LIBRARIES SETUP ###########################
		FIND_PACKAGE(PNG)
		IF(PNG_FOUND)
			TRY_COMPILE(FREEIMAGE_PROVIDES_PNG ${CMAKE_BINARY_DIR}
				${CMAKE_SOURCE_DIR}/cmake/FindFreeImage.cxx
				CMAKE_FLAGS
				"-DINCLUDE_DIRECTORIES:STRING=${PNG_INCLUDES}"
				"-DLINK_LIBRARIES:STRING=${FREEIMAGE_LIBRARY}"
				COMPILE_DEFINITIONS -D__TEST_PNG__)
			IF (NOT FREEIMAGE_PROVIDES_PNG)
				SET(FREEIMAGE_LIBRARIES ${FREEIMAGE_LIBRARIES} ${PNG_LIBRARIES})
			ENDIF(NOT FREEIMAGE_PROVIDES_PNG)
		ELSE(PNG_FOUND)
			MESSAGE(STATUS "Warning : could not find PNG - building without png support")
		ENDIF(PNG_FOUND)
######################### OPENEXR LIBRARIES SETUP ###########################
		FIND_PATH(OPENEXR_INCLUDE_DIRS
			NAMES ImfXdr.h OpenEXRConfig.h
			PATHS
			/usr/local
			/usr
			/sw
			/opt/local
			/opt/csw
			/opt
			PATH_SUFFIXES include/OpenEXR
		)
		IF (OPENEXR_INCLUDE_DIRS)
			TRY_COMPILE(FREEIMAGE_PROVIDES_OPENEXR ${CMAKE_BINARY_DIR}
				${CMAKE_SOURCE_DIR}/cmake/FindFreeImage.cxx
				CMAKE_FLAGS
				"-DINCLUDE_DIRECTORIES:STRING=${OPENEXR_INCLUDE_DIRS}"
				"-DLINK_LIBRARIES:STRING=${FREEIMAGE_LIBRARY}"
				COMPILE_DEFINITIONS -D__TEST_OPENEXR__)
			IF (NOT FREEIMAGE_PROVIDES_OPENEXR)
				SET(FREEIMAGE_LIBRARIES ${FREEIMAGE_LIBRARIES} Half IlmImf Iex Imath)
			ENDIF(NOT FREEIMAGE_PROVIDES_OPENEXR)
		ENDIF (OPENEXR_INCLUDE_DIRS)
	ENDIF(NOT APPLE)
ELSE (FREEIMAGE_INCLUDE_PATH AND FREEIMAGE_LIBRARY)
	SET(FREEIMAGE_FOUND FALSE)
ENDIF (FREEIMAGE_INCLUDE_PATH AND FREEIMAGE_LIBRARY)

MARK_AS_ADVANCED(FREEIMAGE_LIBRARY FREEIMAGE_LIBRARIES FREEIMAGE_INCLUDE_PATH)
